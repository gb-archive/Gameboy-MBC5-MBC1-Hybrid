/*
 Gameboy MBC5 only (For 2MB ROM and 128KB SRAM)
 PCB: v1.1
 Firmware: R1.2a
 Author: Alex from insideGadgets (www.insidegadgets.com)
 Created: 18/04/2018
 Last Modified: 10/06/2019
 License: CC-BY
  
 MBC5 support only
 
*/

module sram_mbc5_2mb_v1_1 (
reset, 
inputAddress, inputData, inputCE, inputWR, inputRD,
highAddress, ramCE);

// Inputs
input reset;
input [3:0] inputAddress; // a15 - a12
input [6:0] inputData; // d6 - d0
input inputCE;
input inputRD;
input inputWR;

// Outputs / Registers
output [7:0] highAddress; // a20 - a13
reg [7:0] highAddress = 8'b0;

reg [6:0] romBank = 7'b1; // a20 - a14 
reg [3:0] ramBank = 4'b0; // a16 - a13 
reg ramEnabled = 1'b0;

output ramCE;
reg ramCE = 1'b1;

// ROM a0-a13 straight through, a14-a21 from CPLD highAddress [1:8]
// RAM a0-a12 straight through, a13-a16 from CPLD highAddress [0:3]
// input RD, WR, CS straight through to ROM
// input RD, WR, straight through to RAM, CE handled by CPLD
// romCE to A15

always @ (reset or inputCE or inputRD or inputWR) begin
	if (!reset) begin
		highAddress <= 8'b0;
		romBank <= 7'b1;
		ramBank <= 4'b0;
		ramEnabled <= 1'b0;
		ramCE <= 1'b1;
	end
	else begin
		// *** ROM Functions ***
		// Only pass through on 0x0000-7FFF if reading or writing flash
		highAddress <= 8'b0;
		if (inputAddress >= 4'd4 && inputAddress <= 4'd7) begin
			highAddress <= (romBank << 1); // Start at a14 for ROM
		end
		
		// 0x2000-3FFF - Low 7 bits of ROM Bank Number (Write Only)
		if (inputAddress == 4'd2 && !inputWR) begin
			romBank <= inputData;
		end
		
		// *** RAM Functions ***
	   ramCE <= 1'b1;
		
		// Only pass through on 0xA000-BFFF if RAM is enabled
		if ((inputAddress == 4'hA || inputAddress == 4'hB) && ramEnabled) begin
			ramCE <= inputCE;
			highAddress <= ramBank;
		end
	
		// 0x0000-1FFF - RAM Enable (Write Only). 0x0A = Enable, 0x00 (or anything else) Disable
		if ((inputAddress == 4'd1 || inputAddress == 4'd0) && !inputWR) begin
			if (inputData == 7'hA) begin // Enable RAM
				ramEnabled <= 1'd1;
			end
			else begin // Disable RAM
				ramEnabled <= 1'd0;
			end
		end
  
		// 0x4000-5FFF - RAM Bank Number (Write Only)
		if ((inputAddress == 4'd4 || inputAddress == 4'd5) && !inputWR) begin
			ramBank <= inputData[3:0];
		end
	end
end

endmodule
